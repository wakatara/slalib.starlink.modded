* EXTRA non-standard slalib routine to convert PV to RA,DEC,R for
*  a given ELONG,PHI
*

      SUBROUTINE sla_PVRD (DATE, ELONG, PHI, HEIGHT, PV, RA, DEC, R)
*+
*     - - - - - - -
*      PVRD
*     - - - - - - -
*
*  Topocentric apparent RA,Dec of a Solar-System object with
*  position velocity vector PV(1..6)
*
*  Given:
*     DATE      d     MJD of observation (JD - 2400000.5)
*     ELONG     d     observer's east longitude (radians)
*     PHI       d     observer's north latitude (radians)
*     HEIGHT    d     observer's height above ref spheroid (m)
*     PV(6)     D     PV vector x,y,z,vx,vy,vz in AU and AU/s
*
*  Returned:
*     RA,DEC    d     RA, Dec (topocentric apparent, radians)
*     R         d     distance from observer (AU)
*
*  Notes:
*
*  1  DATE is the instant for which the prediction is required.  It is
*     in the TT timescale (formerly Ephemeris Time, ET) and is a
*     Modified Julian Date (JD-2400000.5).
*
*  2  The longitude and latitude allow correction for geocentric
*     parallax.  This is usually a small effect, but can become
*     important for Earth-crossing asteroids.  Geocentric positions
*     can be generated by appropriate use of routines slEVP and
*     slPLNE.
*
*  3  The elements are with respect to the J2000 ecliptic and equinox.
*
*
*  Called: slGMST, slDT, slEPJ, slPVOB, slPRNU,
*          slPLNE, slDMXV, slDC2S, slDA2P
*
*  modified from  P.T.Wallace   Starlink   17 March 1999
*
*  Copyright (C) 1999 Rutherford Appleton Laboratory
*  Copyright (C) 1995 Association of Universities for Research in Astronomy Inc.
*-

      IMPLICIT NONE

      DOUBLE PRECISION DATE,ELONG,PHI,HEIGHT,PV(6)
      DOUBLE PRECISION EPOCH,RA,DEC,R

*  Light time for unit distance (sec)
      DOUBLE PRECISION TAU
      PARAMETER (TAU=499.004782D0)

      INTEGER I
      DOUBLE PRECISION DVB(3),DPB(3),VSG(6),VSP(6),V(6),RMAT(3,3),
     :                 VGP(6),STL,VGO(6),DX,DY,DZ,D,TL
      DOUBLE PRECISION sla_GMST,sla_DT,sla_EPJ,sla_DRANRM



*  Sun to geocentre (J2000).
C      CALL sla_EVP(DATE,2000D0,DVB,DPB,VSG(4),VSG)


      CALL sla_EPV(DATE,VSG,VSG(4),DPB,DVB)
      DO I=4,6
          VSG(I)=VSG(I)/86400D0
      END DO


*  Formerly computed sun to planet vector into VSP, but we just copy
*  over input PV now
      DO I=1,6
        VSP(I)=PV(I)
      END DO
      


*  Geocentre to planet (J2000).
      DO I=1,6
         V(I)=VSP(I)-VSG(I)
      END DO



*  Precession and nutation to date.
      CALL sla_PRENUT(2000D0,DATE,RMAT)
      CALL sla_DMXV(RMAT,V,VGP)
      CALL sla_DMXV(RMAT,V(4),VGP(4))

*  Geocentre to observer (date).
      STL=sla_GMST(DATE-sla_DT(sla_EPJ(DATE))/86400D0)+ELONG
      CALL sla_PVOBS(PHI,HEIGHT,STL,VGO)

*  Observer to planet (date).
      DO I=1,6
         V(I)=VGP(I)-VGO(I)
      END DO

*  Geometric distance (AU).
      DX=V(1)
      DY=V(2)
      DZ=V(3)
      D=SQRT(DX*DX+DY*DY+DZ*DZ)

*  Light time (sec).
      TL=TAU*D

*  Correct position for planetary aberration
      DO I=1,3
         V(I)=V(I)-TL*V(I+3)
      END DO

*  To RA,Dec.
      CALL sla_DCC2S(V,RA,DEC)
      RA=sla_DRANRM(RA)
      R=D

 
      END
